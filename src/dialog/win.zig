const std = @import("std");

const win32 = @import("win32").everything;

const windy = @import("../windy.zig");
const Error = @import("errors.zig").WinError;

fn appendFilters(allocator: std.mem.Allocator, dialog: *win32.IFileDialog, filters: []const windy.Filter) Error!void {
    const com_filters = try allocator.alloc(win32.COMDLG_FILTERSPEC, filters.len);
    for (filters, com_filters) |f, *cf| {
        var ext_list: std.ArrayList(u8) = .empty;
        if (f.exts) |exts| {
            for (exts, 0..) |ext, i|
                try ext_list.print(allocator, "*.{s}{s}", .{ ext, if (i == exts.len - 1) "" else ";" });
        } else try ext_list.appendSlice(allocator, "*.*");
        cf.* = .{
            .pszName = try std.unicode.wtf8ToWtf16LeAllocZ(allocator, f.name),
            .pszSpec = try std.unicode.wtf8ToWtf16LeAllocZ(allocator, ext_list.items),
        };
    }
    if (win32.FAILED(dialog.SetFileTypes(@intCast(com_filters.len), com_filters.ptr)))
        return Error.Filter;
}

fn setDefaultPath(allocator: std.mem.Allocator, dialog: *win32.IFileDialog, path: []const u8) Error!void {
    const w_path = try std.unicode.wtf8ToWtf16LeAllocZ(allocator, path);
    var folder: *win32.IShellItem = undefined;
    const path_res = win32.SHCreateItemFromParsingName(w_path, null, win32.IID_IShellItem, @ptrCast(&folder));
    if (path_res == errToHres(.ERROR_FILE_NOT_FOUND) or path_res == errToHres(.ERROR_INVALID_DRIVE))
        return;
    if (win32.FAILED(path_res)) return Error.DefaultPath;
    defer _ = folder.IUnknown.Release();

    if (win32.FAILED(dialog.SetFolder(folder))) return Error.DefaultPath;
}

pub fn openDialog(
    comptime multiple_selection: bool,
    allocator: std.mem.Allocator,
    child_allocator: std.mem.Allocator,
    _: std.Io,
    dialog_type: windy.DialogType,
    filters: []const windy.Filter,
    title: []const u8,
    default_path: ?[]const u8,
) Error!if (multiple_selection) []const []const u8 else []const u8 {
    var dialog: *win32.IFileOpenDialog = undefined;
    if (win32.FAILED(win32.CoCreateInstance(
        win32.CLSID_FileOpenDialog,
        null,
        win32.CLSCTX_ALL,
        win32.IID_IFileOpenDialog,
        @ptrCast(&dialog),
    ))) return Error.Instance;
    defer _ = dialog.IUnknown.Release();

    if (multiple_selection or dialog_type == .directory) {
        var flags: win32.FILEOPENDIALOGOPTIONS = .{};
        if (win32.FAILED(dialog.IFileDialog.GetOptions(@ptrCast(&flags))))
            return Error.Attribute;
        if (multiple_selection) flags.ALLOWMULTISELECT = 1;
        if (dialog_type == .directory) flags.PICKFOLDERS = 1;
        if (win32.FAILED(dialog.IFileDialog.SetOptions(flags)))
            return Error.Attribute;
    }

    try appendFilters(allocator, &dialog.IFileDialog, filters);
    if (default_path) |path| try setDefaultPath(allocator, &dialog.IFileDialog, path);
    if (win32.FAILED(dialog.IFileDialog.SetTitle(try std.unicode.wtf8ToWtf16LeAllocZ(allocator, title))))
        return Error.Title;

    const show_res = dialog.IModalWindow.Show(null);
    if (show_res == errToHres(.ERROR_CANCELLED)) return &.{};
    if (win32.FAILED(show_res))
        return Error.Show;

    if (!multiple_selection) {
        var item: *win32.IShellItem = undefined;
        if (win32.FAILED(dialog.IFileDialog.GetResult(@ptrCast(&item))))
            return Error.Result;
        defer _ = item.IUnknown.Release();

        var file_path: [*:0]u16 = undefined;
        if (win32.FAILED(item.GetDisplayName(win32.SIGDN_FILESYSPATH, @ptrCast(&file_path))))
            return Error.Result;
        defer win32.CoTaskMemFree(file_path);

        return try std.unicode.wtf16LeToWtf8Alloc(child_allocator, std.mem.span(file_path));
    }

    var items: *win32.IShellItemArray = undefined;
    if (win32.FAILED(dialog.GetResults(@ptrCast(&items))))
        return Error.Result;
    defer _ = items.IUnknown.Release();

    var items_len: u32 = 0;
    if (win32.FAILED(items.GetCount(@ptrCast(&items_len))))
        return Error.Result;
    if (items_len == 0) return &.{};

    var ret: std.ArrayList([]const u8) = .empty;
    for (0..items_len) |i| {
        var item: *win32.IShellItem = undefined;
        if (win32.FAILED(items.GetItemAt(@intCast(i), @ptrCast(&item))))
            return Error.Result;

        const sfgao_fs = win32.SFGAO_FILESYSTEM;
        var attribs: u32 = undefined;
        if (win32.FAILED(item.GetAttributes(@intCast(sfgao_fs), @ptrCast(&attribs))) or (attribs & sfgao_fs) == 0)
            return Error.Result;

        var path: [*:0]u16 = undefined;
        if (win32.FAILED(item.GetDisplayName(win32.SIGDN_FILESYSPATH, @ptrCast(&path))))
            return Error.Result;
        defer win32.CoTaskMemFree(path);

        try ret.append(child_allocator, try std.unicode.wtf16LeToWtf8Alloc(child_allocator, std.mem.span(path)));
    }

    return try ret.toOwnedSlice(child_allocator);
}

pub fn saveDialog(
    allocator: std.mem.Allocator,
    child_allocator: std.mem.Allocator,
    _: std.Io,
    filters: []const windy.Filter,
    title: []const u8,
    default_path: ?[]const u8,
) Error![]const u8 {
    var dialog: *win32.IFileSaveDialog = undefined;
    if (win32.FAILED(win32.CoCreateInstance(
        win32.CLSID_FileSaveDialog,
        null,
        win32.CLSCTX_ALL,
        win32.IID_IFileSaveDialog,
        @ptrCast(&dialog),
    ))) return Error.Instance;
    defer _ = dialog.IUnknown.Release();

    try appendFilters(allocator, &dialog.IFileDialog, filters);
    if (default_path) |path| try setDefaultPath(allocator, &dialog.IFileDialog, path);
    if (win32.FAILED(dialog.IFileDialog.SetTitle(try std.unicode.wtf8ToWtf16LeAllocZ(allocator, title))))
        return Error.Title;

    const show_res = dialog.IModalWindow.Show(null);
    if (show_res == errToHres(.ERROR_CANCELLED)) return &.{};
    if (win32.FAILED(show_res))
        return Error.Show;

    var item: *win32.IShellItem = undefined;
    if (win32.FAILED(dialog.IFileDialog.GetResult(@ptrCast(&item))))
        return Error.Result;
    defer _ = item.IUnknown.Release();

    var file_path: [*:0]u16 = undefined;
    if (win32.FAILED(item.GetDisplayName(win32.SIGDN_FILESYSPATH, @ptrCast(&file_path))))
        return Error.Result;
    defer win32.CoTaskMemFree(file_path);

    return try std.unicode.wtf16LeToWtf8Alloc(child_allocator, std.mem.span(file_path));
}

pub fn message(
    allocator: std.mem.Allocator,
    _: std.Io,
    level: windy.MessageLevel,
    buttons: windy.MessageButtons,
    text: []const u8,
    title: []const u8,
) Error!bool {
    var style: win32.MESSAGEBOX_STYLE = .{};
    switch (level) {
        .info => style.ICONASTERISK = 1,
        .warn => {
            style.ICONHAND = 1;
            style.ICONQUESTION = 1;
        },
        .err => style.ICONHAND = 1,
    }

    switch (buttons) {
        .yes_no => style.YESNO = 1,
        .ok_cancel => style.OKCANCEL = 1,
        .ok => {},
    }

    const res = win32.MessageBoxW(
        win32.GetActiveWindow(),
        try std.unicode.wtf8ToWtf16LeAllocZ(allocator, text),
        try std.unicode.wtf8ToWtf16LeAllocZ(allocator, title),
        style,
    );
    return res == .OK or res == .YES;
}

pub fn colorChooser(
    _: std.mem.Allocator,
    _: std.Io,
    color: windy.Rgba,
    _: bool,
    _: []const u8,
) Error!?windy.Rgba {
    var custom_colors: [16]u32 = @splat(0xFFFFFF);

    var choose_color = std.mem.zeroes(win32.CHOOSECOLORW);
    choose_color.lStructSize = @sizeOf(win32.CHOOSECOLORW);
    choose_color.hwndOwner = win32.GetActiveWindow();
    choose_color.rgbResult = color.toColor();
    choose_color.lpCustColors = @ptrCast(&custom_colors);
    choose_color.Flags = @bitCast(win32.CHOOSECOLOR_FLAGS{
        .RGBINIT = 1,
        .ANYCOLOR = 1,
        .FULLOPEN = 1,
    });
    if (win32.ChooseColorW(&choose_color) == 0) return null;
    return .fromColor(choose_color.rgbResult, 1.0);
}

const HresUint = std.meta.Int(.unsigned, @typeInfo(win32.HRESULT).int.bits);
fn errToHres(err: win32.WIN32_ERROR) win32.HRESULT {
    const hr: HresUint = (@as(HresUint, @intFromEnum(err)) & 0x0000FFFF) |
        (@as(HresUint, @intFromEnum(win32.FACILITY_WIN32)) << 16) |
        @as(HresUint, 0x80000000);
    return @bitCast(hr);
}
